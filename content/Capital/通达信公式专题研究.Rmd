---
author:
  - 通达信公式
title: '通达信公式'
categories:
  - 通达信
  - 选股策略
tags:
  - 通达信
  - 选股策略
date: 2021-12-30
slug: TDX
weight: 001
description: <font face="思源宋体 CN" >通达信公式研究系列</font>
---



# 关键概念

## 公式编辑器

<font face="思源宋体 CN" >

　　“ 公式编辑器 ” 好比是一个工作母床，通过这个工作母床可以制造出所需要的各式各样的零件，同样，在指标分析的工作中，利用编辑器可以编写出相应的分析条件，这种方法是在技术分析当中最为常用的方法之一。例如，指标 KD 、指标 MA 等等，通过对这些指标的观察、分析，找出一些合适的条件作为买入卖出点。当然，我们也许需要的是一些自己的指标，一些自己的准确的指标，更多的 MB 、 MC 、 MD 等等，这一切我们通过 “ 公式编辑器 ” 可以实现。

　　**公式管理器可管理四种类型的公式：**  

- 技术指标公式：用于指标图形的绘制。  
- 条件选股公式：用于条件选股。   
- 专家系统公式：用于专家指示及测参、测指。   
- 五彩K线公式：用于K线模式指示。    
- 这四种公式相互独立，名称可以相同，但其内容和作用不同。   

### 什么是技术指标公式编辑器？   

　　MA 均线就是一种技术指标，我们在炒股的时候，经常会将一些行情数据进行数学计算得出一些曲线等等，方便我们掌握股市的变动情况。  

　　**注意事项**：


- 1 、如果选择： “ 主图叠加 ” ，我们的指标线会与 K 线图显示在同一个图形框中，现在我们的指标线显示在其下方，即 “ 副图 ” 中；    
- 2 、参数可以有，也可以没有，但是鼓励大家设置参数，这是非常好的习惯；   

- 3 、一个句子完了，别忘了以分号结尾；
你的公式写得对不对，可以通过 “ 测试公式 ” 来检查，如果错了，它会告诉你错在哪里。

### 什么是条件选股公式编辑器？   

　　简单讲，就是按照您的设定的条件用电脑帮助您完成一些太多太复杂的挑选。比如您有一些好的心得和方法，可使有 1000 多只股票，您就是有 100 双眼睛有时也不一定可以看得过来，这时电脑就派上用场了！

> 1 、指标和条件选股在结构上没有差别，只是在内容上，条件选股要多加上我们的条件，比如大于 10 ，或者交叉等等！  
2 、中间表达式可以帮助我们清晰的表达我们的公式，不至于使公式的结构特别的混乱！


### 什么是画线指标公式编辑器

- ［公式名称］用于识别指标公式，不能重名，最多可以有16个字符。键盘精灵也用该名称来选择公式。
- ［公式描述］简单描述该指标的用途。
- ［密码保护］如果其前未打√，表示该公式没加密。点击"密码保护"，使其打√选中，此时输入密码，公式就被加密。对于已加密的公式，必须输入密码才能看到公式的内容，要去掉密码，点击"密码保护"，使它前面的√消失即可。
- ［指标类型］指定指标公式类型（共分为15种）。
- ［画线方法］紧密围绕股票价格变化的指标可定为主图叠加类型，叠加在主图上显示；否则定为副图指标，只能显示在副图上。
- ［参数名称］公式可以带参数，参数的名字在这里输入。
- ［最小值、最大值、缺省值］参数的最小值、最大值和默认值（缺省值）在这里输入。
- ［坐标线位置］仅对副图指标有效，可指定在哪些位置画水平坐标线，例如对BIAS，可以输入：-20；0；20； 系统将在这三个数值处绘制三条水平坐标线。缺省时为"自动"，即系统据实际情况定水平坐标线。
- ［额外Y轴分界］
- ［引入指标公式］以系统已有的某个指标公式为蓝本，新建自己的指标公式。
- ［插入函数］显示选择函数对话框，帮助您在《通达信集成版》软件函数集中寻找所需函数。当您对系统提供的标准函数还不熟时，这个功能可帮你节省时间。

- 　Tdxw3.0将函数类型分为：
全部函数 行情函数 时间函数 引用函数 逻辑函数
数学函数 统计函数 指数函数 大盘函数 常数函数
内嵌函数 操作符函数
- 　[动态提示]用户输入指标、算法或函数时，同步提示中文语意。
- [测试公式]公式编写完毕后检查公式的语法错误。
- [测试结果]测试完毕后提示"测试通过"或显示错误信息。
- [参数精灵]用户新建公式时，可在参数精灵中用一段文字来描述待设置参数的意义，这段解释性文字会在允许用户调整参数值的地方出现，包括条件选股、指标排序、调整指标参数、选择指标、选择交易系统指示、选择条件选股指示等等。有了这段解释性文字的指导，用户就不会盲目操作了。



### 什么是交易系统公式编辑器           

　　在新建、修改交易系统公式时，将弹出交易系统公式编辑器对话框。
比较指标公式编辑器我们只对多出的部分进行讲解。
- ［测试步长］在进行参数优化时，每次参数变化的大小。
- ［多头买入］做多时的买入条件在此输入，买入点指示的颜色及式样也请在此选定。
- ［多头卖出］　做多时的卖出条件在此输入，卖出点指示的颜色及式样也请在此选定。
- ［空头买入］　做空时的买入条件在此输入，买入点指示的颜色及式样也请在此选定。
- ［空头卖出］做空时的卖出条件在此输入，卖出点指示的颜色及式样也请在此选定。

### 什么是五彩K线公式编辑器         




　　 与指标公式编辑器的使用相似


### 什么是参数？   

　　比如讲： 10 日均线，您可以把 10 日当作参数，好处在于，您觉得需要修改成 5 日的时候，就可以使用一些简单的方法，例如参数精灵来很方便的修改和调整。参数需要名字，例如 M 就不错。还要规定参数的范围，例如 1 日至 260 日。这样我们就可以在 1 到 260 之间任意调节 M 的值了， M 最常用的数填在 “ 缺省 ” 一栏，例如你最喜欢用 10 日均线，那就填 10 吧。

### 什么是周期

　　这么解释吧！我们有的投资者喜欢使用日线图作技术分析；有的喜欢用 5 分钟的 K线；有的喜欢使用长一点时间的，例如周线。所以在公式设计中，允许不同喜好的使用者选择不同的分析时间 ，就是可以选择不同的周期。

## 什么是函数

　　函数在公式编写非常重要，如果作个比喻，我们用一种语言去告诉电脑我的想法，并且让它去帮我做，那么函数就是这种语言的单词。
我们在公式编辑器中选择插入函数，就可以看到里面有许多的函数，我们在附录中有一个简表，大家可以到那里去检索！

>[通达信函数链接](https://www.tdx.com.cn/products/helpfile/tdxw/index.html)

## 重要函数

###  行情函数

- `HIGH`或`H`，最高价，返回该周期最高价。

- `LOW`或`L`， 最低价，返回该周期最低价。

- `CLOSE`或`C`， 收盘价，返回该周期收盘价。

- `VOL`或`V`， 成交量（手），返回该周期成交量。

- `OPEN`或`O`， 成交量（手），返回该周期开盘价。

###  逻辑函数

- `CROSS`, 上穿。用法:
`CROSS(A,B)`表示当A从下方向上穿过B时返回1,否则返回0。例如:
`CROSS(MA(CLOSE,5),MA(CLOSE,10))`表示5日均线与10日均线交金叉


- `LONGCROSS`, 维持周期后上穿，两条线维持一定周期后交叉。用法:
`LONGCROSS(A,B,N)`表示A在N周期内都小于B,本周期从下方向上穿过B时返回1,否则返回0。


- `UPNDAY`, 连涨，返回是否连涨周期数。用法:
`UPNDAY(CLOSE,M)`表示连涨M个周期,M为常量。


- `DOWNNDAY`, 连跌，返回是否连跌周期。用法:
`UPNDAY(CLOSE,M)`表示连跌M个周期,M为常量。


- `NDAY`, 连大，返回是否持续存在X>Y。用法:
`NDAY(CLOSE,OPEN,3)`表示连续3日收阳线。

- `EXIST`, 存在，是否存在。用法:
`EXIST(CLOSE>OPEN,10)`表示10日内存在着阳线。


- `EXISTR`, 存在，EXISTR(X,A,B):是否存在(前几日到前几日间)。用法:
`EXISTR(CLOSE>OPEN,10,5)`表示从前10日内到前5日内存在着阳线
若A为0,表示从第一天开始,B为0,表示到最后日止。


- `EVERY`, 一直存在，用法:
`EVERY(CLOSE>OPEN,N)`表示N日内一直阳线(N应大于0,小于总周期数,N支持变量)。

- `LAST`, 持续存在，用法:
`LAST(X,A,B)`，持续存在。`LAST(CLOSE>OPEN,10,5)`表示从前10日到前5日内一直阳线
若A为0,表示从第一天开始,B为0,表示到最后日止。

- `NOT`, 取反，用法:
`NOT(X)`返回非X,即当X=0时返回1,否则返回0。`NOT(ISUP)`,表示平盘或收阴。

### 引用函数  

#### 引用扩展数据：EXTDATA_USER 

　　引用扩展数据。请用扩展数据管理器来设置和刷新数据。


　　EXTDATA_USER(N,TYPE),N取(1-100),表示读当前品种的N号扩展序列数据,TYPE:1表示做平滑处理,没有数据的周期返回上一周期的值;0表示不做平滑处理;2表示没有数据则为0。

#### 日前的：REF       

　　引用若干周期前的数据（平滑处理）

　　用法：REF(X , A) 引用 A 周期前的 X 值。A可以是变量 。 ( 平滑处理：当引用不到数据时进行的操作。此函数中，平滑时使用上一周期的引用值。 )

　　例如：`REF(CLOSE ,BARSCOUNT(C)-1) `表示第二根 K 线的收盘价。

#### 日前的（未作平滑处理）：REFV       

　　引用若干周期前的数据（未作平滑处理）

　　用法：REFV(X , A) 引用 A 周期前的 X 值。A可以是变量。( 平滑处理：当引用不到数据时进行的操作。)

　　例如：`REFV(CLOSE , BARSCOUNT(C)-1) `表示第二根K线的收盘价

#### 统计：COUNT

　　统计满足条件的周期数。用法:  

　　`COUNT(X,N)`,统计N周期中满足X条件的周期数,若N<0则从第一个有效值开始。
例如:`COUNT(CLOSE>OPEN,20)`表示统计20周期内收阳的周期数。

#### 最高值：HHV  

　　求最高值。用法：`HHV(X,N) `求 N 周期内 X 最高值，N=0 则从第一个有效值开始。  

　　例如：`HHV(HIGH,30)`表示求30日最高价。


#### 最低值：LLV    

　　求最低值。    

　　用法：LLV(X,N) 求 N 周期内 X 最低值， N=0 则从第一个有效值开始。   

　　例如：LLV(LOW,0) 表示求历史最低价。  

#### 上一高点位置：HHVBARS  

　　求上一高点到当前的周期数。

　　用法：HHVBARS(X,N) 求 N 周期内X最高值到当前周期数,N=0 表示从第一个有效值开始统计。

　　例如： `HHVBARS(HIGH,0)` 求得历史新高到到当前的周期数。



#### 上一低点位置：LLVBARS       

　　求上一低点到当前的周期数。    

　　用法：LLVBARS(X,N) 求 N 周期内 X 最低值到当前周期数， N=0 表示从第一个有效值开始统计。   

　　例如：LLVBARS(HIGH,20) 求得 20 日最低点到当前的周期数。 


#### 高值名次：HOD   

　　求高值名次，用法： `HOD(X,N)` 求当前 X 数据是 N 周期内的第几个高值 ,N=0 则从第一个有效值开始。

　　例如： `HOD(HIGH,20)` 返回是 20 日的第几个高价。


#### 低值名次：LOD       

　　求低值名次，用法： LOD(X,N) 求当前 X 数据是 N 周期内的第几个低值， N=0，则从第一个有效值开始

　　例如： LOD(LOW,20)返回是 20 日的第几个低价 



#### 求相反数：REVERSE       

　　求相反数。用法：　 REVERSE(X) 返回 -X 。

　　例如：　 REVERSE(CLOSE) 返回 -CLOSE 。





#### 日后的：REFX       

　　属于未来函数,引用若干周期后的数据(平滑处理)。    

　　用法:REFX(X,A),引用A周期后的X值.A可以是变量。   

　　平滑处理:当引用不到数据时进行的操作.此函数中,平滑时使用上一个周期的引用值.
例如:    
```
TT:=IF(C>O,1,2);  
REFX(CLOSE,TT);    
```

　　表示阳线引用下一周期的收盘价,阴线引用日后第二周期的收盘价。  


#### 日后的 (未作平滑处理)：REFXV         

　　属于未来函数,引用若干周期后的数据(未作平滑处理)。   

　　用法:  

　　`REFXV(X,A)`,引用A周期后的X值.A可以是变量。  

　　平滑处理:当引用不到数据时进行的操作。  

　　例如:  

　　`REFXV(CLOSE,1)`表示下一周期的收盘价,在日线上就是明天收盘价。


#### 简单移动平均：MA         

　　返回简单移动平均。

　　用法：　 MA(X,N):X的N日简单移动平均,算法(X1+X2+X3+...+Xn)/N,N支持变量


#### 移动平均：SMA         

　　返回移动平均。

　　用法： SMA(X,N,M):X的N日移动平均,M为权重,如Y=(X*M+Y'*(N-M))/N


#### 移动平均：TMA         

　　返回移动平均

　　用法： TMA(X,A,B),A和B必须小于1,算法 Y=(A*Y'+B*X),其中Y'表示上一周期Y值.初值为X


#### 平滑移动平均：MEMA         

　　返回平滑移动平均

　　用法：

　　`MEMA(X,N)`:X的N日平滑移动平均,如Y=(X+Y'*(N-1))/N；


　　`MEMA(X,N)`相当于`SMA(X,N,1)`。


#### 指数移动平均：EMA          

　　返回指数移动平均。

　　用法：`EMA(X ,N) `,X 的 N日指数移动平均。算法 :Y=(X*2+Y'*(N-1))/(N+1)。

　　`EMA(X,N)`相当于`SMA(X,N+1,2)`,N支持变量。








# 选股公式     


## RPS强势个股

```
{根据最近10日的RPS进行筛选强势个股}

{计算50日RPS}
K10 := EXTDATA_USER(3, 0);
RPSK10 := K10/10;


{计算120日RPS}
K20 := EXTDATA_USER(4, 0);
RPSK20 :=K20/10;



{筛选近10日RPSK120>90;RPSK250>90;}
RPSK10 > 0 AND RPSK20 > 0 AND COUNT(RPSK10 < R, D) = 0 AND COUNT(RPSK20 < R, D) = 0;
```



## RPS强势板块

```
{强势板块分析，最近5日，10日RPS，交易日数据之后一天取值}

{计算板块10日RPS}
K10 := REF(EXTDATA_USER(6, 0),1);

RPSK10 := K10/10;
{计算板块20日RPS}
K20 := REF(EXTDATA_USER(7, 0),1);

RPSK20 :=K20/10;

{筛选近10日RPSK120>90;RPSK250>90;}
RPSK10 > 0 AND RPSK20 > 0 AND COUNT(RPSK10 < R, D) = 0 AND COUNT(RPSK20 < R, D) = 0; = 0;
```
![](https://gitee.com/shao818/Figure/raw/master/截图_20211931101911.png)

筛选结果示例：

![](https://gitee.com/shao818/Figure/raw/master/截图_20212031102013.png)

## 底部横盘突破

```
V1 := MA(VOL, 5); {5日均量}
V2 := VOL / REF(V1, 1) > 2; {今日成交量大于昨日的5日均量的2倍}

PZ1 := MA(CLOSE, M); {今日的M日收盘均价}
PZ2 := HHV(HIGH, M); {M日最高价}
PZ3 := LLV(LOW, M); {M日最低价}
PZ4 := (PZ2 - PZ1) / PZ1; {PZ2和PZ1价格差的百分比}
PZ5 := (PZ1 - PZ3) / PZ1; {PZ1和PZ3价格差的百分比}
PZ := REF(PZ4, 1) < 0.15 AND REF(PZ5, 1) < 0.15; {昨日的PZ4小于0.15且昨日的PZ5小于0.15}

TP1 := HHV(HIGH, M); {M日最高价}
TP := HIGH = TP1; {今日最高价是M日最高价}


V2 AND PZ AND TP; {同时满足V2、PZ、TP三个条件};
```

![](https://gitee.com/shao818/Figure/raw/master/截图_20212131102151.png)

>不要在交易日的交易时间筛选，因为受当天数据限制，筛选结果可能为空。

## 成长性突破

```
{根据近两年营业收入与净利润增长率筛选}

{近两年营业收入和扣非净利润同比增长率大于5%且小于30%}

{近两年营业总收入}

INCOME1 := FINONE(183,Y-1,1231) > 5; {Y年年报净利润增速大于5%}
INCOME11 := FINONE(183,Y-1,1231) < 30; {Y年年报净利润增速小于30%,确保小幅增加}

INCOME2 := FINONE(183,Y-2,1231) > 5; {Y-1年年报净利润增速大于5%}
INCOME22 := FINONE(183,Y-2,1231) < 30; {Y-1年年报净利润增速小于30%}

{近两年扣非净利润}

PROFIT1 := FINONE(191,Y-1,1231) >5; {Y年年报净利润增速大于5%}
PROFIT11 := FINONE(191,Y-1,1231) < 30; {Y年年报净利润增速小于30%}

PROFIT2 := FINONE(191,Y-2,1231) >5; {Y-1年年报净利润增速大于5%}

PROFIT22 := FINONE(191,Y-2,1231) <30; {Y-1年年报净利润增速小于30%}

{最近一期季报：营业收入与扣非净利润均大于30%}

INCOME3 := FINONE(183,K,Z) > 30; {近期财报营业收入大于30，Z和K为0表示最近一期}
PROFIT3 := FINONE(191,K,Z) >30;  {近期财报扣非净利润增速大于30%，Z和K为0表示最近一期}


PROFIT4 :=FINONE(2,K,Z) >0;  {近期财报扣非每股收益大于零，Z和K为0表示最近一期}

INCOME1 AND INCOME11 AND  INCOME2 AND INCOME22 AND PROFIT1 AND PROFIT11 AND PROFIT2 AND PROFIT22 AND INCOME3 AND  PROFIT3 AND PROFIT4; {同时符合四个条件};
```

![](https://gitee.com/shao818/Figure/raw/master/截图_20213131103136.png)

## 收盘价创一年新高

```
{VOL1 :=V/REF(V,1)>Y;相对昨天放量1.5倍}

COL1 :=CLOSE=HHV(HIGH,D);{收盘价创新高}

COL1;
```

## 翻倍高成长

```
{动量因子：成交量翻倍}
VOL1 :=V/REF(V,1)>D;{相对上一周期成交量上涨50%}
{上涨幅度较前一周期翻倍}
COL1 :=(CLOSE/REF(CLOSE,1)-1)>ABS(REF(CLOSE,1)/REF(CLOSE,2)-1)*K;{本周期涨幅是上一周期涨幅的2倍}




{成长因子：最近一期季报营业收入与扣非净利润均大于30%}

INCOME3 := FINONE(183,Y,Z) > 30; {近期财报营业收入大于30%}
PROFIT3 := FINONE(191,Y,Z) >30;  {近期财报扣非净利润增速大于30%}


PROFIT4 :=FINONE(2,Y,Z) >0;  {近期财报扣非每股收益大于零}


VOL1 AND COL1 AND INCOME3 AND PROFIT3 AND PROFIT4;
```

## 口袋支点

```
{高开有缺口}
OPEN1 := OPEN>REF(CLOSE,1);
{涨停}
CLOSE1:= CLOSE>1.095*REF(CLOSE,1);


OPEN1 AND CLOSE1;
```


# 指标公式





## RPS指标  
 
 

### 第一步：建立指标计算年度个股涨幅

```
EXTRS:(C-REF(C,N))/REF(C,N);
```

![](https://gitee.com/shao818/Figure/raw/master/截图_20215531085534.png)


### 第二步:创建扩展数据

>扩展数据就是根据个股年度涨跌幅进行排名，并对排名按照0-10000作归一化处理,即涨幅最大的股票取值为1000，
这也是为什么创建RPS指标公式时将数据指标除以10的原因所在。


![](https://gitee.com/shao818/Figure/raw/master/截图_20215931085925.png)

>个人的几个扩展数据如下：

![](https://gitee.com/shao818/Figure/raw/master/截图_20215031095029.png)


### 第三步:创建个股RPS指标

此部分分为以下几点：

- 首先，引用刚刚创建的扩展数据，需要注意不同周期的RPS曲线与引用的数据要一致。

- 其次，绘制RPS虚线（请注意将对应扩展数据除以10，因为前边排名是0-1000的归一化处理）
，命令说明如下：

  - `:=`，表示赋值；

  - `EXTDATA_USER(5,0)`，表示引用序号为5的扩展数据；参数5表示扩展数据序号，参数0表示是当天的数据，如果是昨天的就取值为1,前天的就取值为2；
  -  `:`，表示输出，通常在创建技术指标时使用；

  - `LINETHICK2`，表示线宽；

  - `COLORGREEN`，表示线的颜色；

  - `IF(RPS10>=M,RPS10,DRAWNULL),LINETHICK2,COLORRED`，表示如果RPS10大于等于M则画红线，否则不划线。`DRAWNULL`返回无效函数。


```
W:=EXTDATA_USER(5,0);{10天的}
RPS10:W/10,LINETHICK2,COLORGREEN;
IF(RPS10>=M,RPS10,DRAWNULL),LINETHICK2,COLORRED;

U:=EXTDATA_USER(4,0);{20天的}
RPS20:U/10,LINETHICK2,COLORWHITE;
IF(RPS20>=M,RPS20,DRAWNULL),LINETHICK2,COLORRED;

Z:=EXTDATA_USER(3,0);{50天的}
RPS50:Z/10,LINETHICK2,COLORYELLOW;
IF(RPS50>=M,RPS50,DRAWNULL),LINETHICK2,COLORRED;

X:=EXTDATA_USER(1,0);{120天的}
RPS120:X/10,LINETHICK2,COLORLICYAN;
IF(RPS120>=M,RPS120,DRAWNULL),LINETHICK2,COLORRED;

Y:=EXTDATA_USER(2,0);{250天的}
RPS250:Y/10,LINETHICK2,COLORBROWN;
IF(RPS250>=M,RPS250,DRAWNULL),LINETHICK2,COLORRED;
```

![](https://gitee.com/shao818/Figure/raw/master/截图_20212131092147.png)


## 港资持股曲线指标

```
NOTEXT曲线:GPJYVALUE(6,1,1),COLORFFFF00;

IF((GPJYVALUE(6,1,1)/FINANCE(7))>0.01,NOTEXT曲线,DRAWNULL),LINETHICK1,COLORRED;

IF((GPJYVALUE(6,1,1)/FINANCE(7))=0,NOTEXT曲线,DRAWNULL),LINETHICK1,COLORWHITE;

IF((GPJYVALUE(6,1,1)/FINANCE(7))>0 AND (GPJYVALUE(6,1,1)/FINANCE(7))<0.01,NOTEXT曲线,DRAWNULL),LINETHICK1,COLORCYAN;

持股量:GPJYVALUE(6,1,1)/10000,COLORLIBLUE;

万股:DRAWNULL;

占流通比:(GPJYVALUE(6,1,1)/FINANCE(7))*100,NODRAW;

占总股比:(GPJYVALUE(6,1,1)/FINANCE(1))*100,NODRAW;

增减万股:(GPJYVALUE(6,1,1)-REF(GPJYVALUE(6,1,1),1))/10000;

增减:=GPJYVALUE(6,1,1)-REF(GPJYVALUE(6,1,1),1);

IF(增减<0,增减,DRAWNULL),STICK,LINETHICK4,COLORCYAN;

IF(增减=0,增减,DRAWNULL),STICK,LINETHICK4,COLORWHITE;

IF(增减>0,增减,DRAWNULL),STICK,LINETHICK4,COLORRED;

```
